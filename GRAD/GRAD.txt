Lbl MS
ClrHome
Menu("Setup","A eingeben",IA,"b eingeben",IB,"xsmall0 eingeben",IC,"starten",GO)


Lbl IA
Input "Matrix A=",[A]
ClrHome
Disp "Matrix A="
Pause [A]>Frac
Goto MS


Lbl IB
Input "Rechte Seite b=",[B]
ClrHome
Disp "Rechte Seite b="
Pause [B]>Frac
Goto MS


Lbl IC
Input "Startwert xsmall0=",[C]
ClrHome
Disp "Startwert xsmall0="
Pause [C]>Frac
Goto MS


" Arbeitsvektor und Dimension initialisieren
Lbl GO
[C]->[D]
dim([D])->|LDIM:|LDIM(1)->N


" Nullvektor initialisieren
[D]->[G]
Fill(0,[G])


" Indizes bestimmen
0->K
"small0"->Str0
Lbl IT
K+1->K
Str9->Str8
Str0->Str9
If K=1:"small1"->Str0
If K=2:"small2"->Str0
If K=3:"small3"->Str0
If K=4:"small4"->Str0
If K=5:"small5"->Str0


0->C
Menu("Verfahren?","konjugiert",C,"gewoehnlich",G,"beenden",E)
Lbl C
1->C
Lbl G


Disp "Residuum:"
If K=1:Then
	[B]-[A][D]->[J]
	Disp "r"+Str9+"=b-Ax"+Str9+"="
Else
	[J]-Q[H]->[J]
	If C=1:Disp "r"+Str9+"=r"+Str8+"-alpha"+Str9+"a"+Str9+"="
	If C=0:Disp "r"+Str9+"=r"+Str8+"-lambda"+Str8+"a"+Str8+"="
End
Pause [J]
If [J]=[G]:Return
R->S
det([J]^^T[J])->R
If C=1:Then
	Disp "rho"+Str9+"=<r"+Str9+",r"+Str9+">"+"="
	Pause R>Frac
End
ClrHome


If C=1:Disp "Suchrichtung:"
If K=1:Then
	[J]->[I]
	If C=1:Disp "d"+Str0+"=r"+Str9+"="
Else
	If C=1:[J]+(R/S)[I]->[I]
	If C=0:[J]->[I]
	If C=1:Disp "d"+Str0+"=r"+Str9+"+(rho"+Str9+"/rho"+Str8+")d"+Str9+"="
End
If C=1:Pause [I]
ClrHome


Disp "Line search:"
[A][I]->[H]
If C=1:Disp "a"+Str0+"=Ad"+Str0+"="
If C=0:Disp "a"+Str9+"=Ar"+Str9+"="
Pause [H]
R/det([I]^^T[H])->Q
If C=1:Disp "alpha"+Str0+"=rho"+Str9+"/<d"+Str0+",a"+Str0+">"+"="
If C=0:Disp "lambda"+Str9+"=<r,r>/<r,a>"+"="
Pause Q>Frac
ClrHome


Disp "Neue Iterierte:"
[D]+Q[I]->[D]
If C=1:Disp "x"+Str0+"=x"+Str9+"+alpha"+Str0+"d"+Str0+"="
If C=0:Disp "x"+Str0+"=x"+Str9+"+lambda"+Str9+"r"+Str9+"="
Pause [D]
ClrHome
Goto IT


Lbl E
