Lbl MS
ClrHome
Menu("Setup","\Phi eingeben",IP,"starten",GO,"L eingeben",IL)


Lbl IP
Input "\Phi(X)=",Str0
String>Equ(Str0,{Y1})
Goto MS


Lbl IL
Input "Lipsc"+"hitz L=",L
Goto MS


Lbl GO
Input "Startwert xsmall0=",D
ClrHome


" Indizes bestimmen
0->K
"small0"->Str0
Lbl IT
K+1->K
Str9->Str8
Str0->Str9
If K=1:"small1"->Str0
If K=2:"small2"->Str0
If K=3:"small3"->Str0
If K=4:"small4"->Str0
If K=5:"small5"->Str0


" Werte rotieren und neue Iterierte berechnen
B->A
C->B
D->C
{Y1}(D)->D
Disp "x"+Str0+"=Phi(x"+Str9+")=",D


Lbl MA
Pause 
ClrHome
If K=1:Menu("Aktion?","iterieren",IT,"a-priori",PR,"a-posteriori",PO,"beenden",E)
If K=2:Menu("Aktion?","iterieren",IT,"a-priori",PR,"a-posteriori",PO,"beschleunigen",AB,"L berechnen",L,"beenden",E)
If K>2:Menu("Aktion?","iterieren",IT,"a-priori",PR,"a-posteriori",PO,"beschleunigen",AB,"L berechnen",L,"p berechnen",P,"beenden",E)


Lbl PR
Input "Toleranz epsilon=",E
Disp "Schritte k>=",ln((1-L)E/abs(D-C))/ln(L)+K-1
Goto MA


Lbl PO
Disp "|x"+Str0+"-x*|"+"<=","L/(1-L)|x"+Str0+"-x"+Str9+"|=",abs(D-C)L/(1-L)
Goto MA


Lbl AB
Disp "Aitken x=",(C^2-B*D)/(2*C-D-B)
Goto MA


Lbl L
abs(D-C)/abs(C-B)->L
Disp "Lipsc"+"hitz L=","|x"+Str0+"-x"+Str9+"|/|x"+Str9+"-x"+Str8+"|=",L
Goto MA


Lbl P
Disp "Konvergenzor. p=",ln(abs((C-D)/(B-D)))/ln(abs((B-D)/(A-D)))
Goto MA


Lbl E
