Lbl MS
ClrHome
Menu("Setup","f eingeben",IF,"starten",GO)


Lbl IF
Input "f(T,Y)=",Str0
String>Equ(Str0,{Y1})
Goto MS


Lbl GO
Input "Start  tsmall0=",A
Input "Schritt h=",H
Input "Stufen  m=",M
{M,1}->dim([G])
{M+1,1}->dim([H])
{M,1}->dim([I])
{M,1}->dim([J])


" alpha, beta und y eingeben
For(I,1,3)
	"small0"->Str0
	ClrHome
	For(J,1,M+1)
		If I=1:Then
			Input "alpha"+Str0+"=",B
			B->[H](J,1)
		End
		If I=2 and J<=M:Then
			Input "beta"+Str0+"=",B
			B->[I](J,1)
		End
		If I=3 and J<=M:Then
			Input "y"+Str0+"=y(x"+Str0+")=",B
			B->[J](J,1)


			" f berechnen
			A+(J-1)H->T
			[J](J,1)->Y
			{Y1}->[G](J,1)
			Disp "","f"+Str0+"=f(t"+Str0+",y"+Str0+")="
			Pause [G](J,1)
			ClrHome
		End
		If J=1:"small1"->Str0
		If J=2:"small2"->Str0
		If J=3:"small3"->Str0
		If J=4:"small4"->Str0
		If J=5:"small5"->Str0
	End
End


Lbl MA
ClrHome
Menu("Aktion?","Zeitschritt",ZS,"Konsistenzord.",KO,"Stabilitaet",ST,"Ende",E)


Lbl ZS
0->Z
For(J,1,M)
	Z-[J](J,1)[H](J,1)+H[I](J,1)[G](J,1)->Z
End


" y- und f-Werte rotieren
For(J,2,M)
	[J](J,1)->[J](J-1,1)
	[G](J,1)->[G](J-1,1)
End


" Neue Werte berechnen
Z/[H](M+1,1)->[J](M,1)
T+H->T
[J](M,1)->Y
{Y1}->[G](M,1)
Disp "y"+Str0+"=",[J](M,1),"","f"+Str0+"=f(t"+Str0+",y"+Str0+")=",[G](M,1)
Pause 
Goto MA


Lbl KO
0->P
While 1=1
	Disp "Teste p=",P


	" alpha-Summe
	0->S
	For(J,1,M+1)
		If P=0:S+[H](J,1)->S
		If P>0:S+(J-1)^P[H](J,1)->S
	End
	If P=0:Disp "Sigmaalpha="
	If P=1:Disp "Sigmalalpha="
	If P=2:Disp "Sigmal^^2alpha="
	If P=3:Disp "Sigmal^^3alpha="
	If P>3:Disp "Sigmal^qalpha="
	Disp S
	If P=0:Then
		If S!=0:Then
			Pause "!=0"
			Goto MA
		End
		Pause "Weiter"
	Else


		" beta-Summe
		0->R
		For(J,1,M)
			If P=1:R+P[I](J,1)->R
			If P>1:R+P(J-1)^(P-1)[I](J,1)->R
		End
		If P=1:Disp "qSigmabeta="
		If P=2:Disp "qSigmalbeta="
		If P=3:Disp "qSigmal^^2beta="
		If P>3:Disp "qSigmal^(q-1)beta="
		Disp R

		If S!=R:Then
			Pause "Abbruch"
			Goto MA
		End
		Pause "Weiter"
	End
	P+1->P
	ClrHome
End


Lbl ST
"0"->Str0
For(J,1,M+1)
	If J=1:"1"->Str1
	If J=2:"2"->Str1
	If J=3:"3"->Str1
	If J=4:"4"->Str1
	If J=5:"5"->Str1
	Str0+"+[H]("+Str1+",1)X^("+Str1+"-1)"->Str0
End
String>Equ(Str0,{Y0})
FnOff 
FnOn 0
DispGraph


Lbl E
