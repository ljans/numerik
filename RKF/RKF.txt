ClrHome

Menu("Verfahren?","[A],[B],[C]",OK,"Euler explizit",1,"Euler besser",3,"Heun",2,"Ralston",4,"RK klassisch",5,"ode23",6)

" BUTCHER-TABLEAU IN MATRIZEN [A], [B], [C] SPEICHERN

" Explizites Euler-Verfahren
Lbl 1
[[0]]->[A]
[[0]]->[B]
[[1]]->[C]
Goto OK

" Verfahren von Heun
Lbl 2
[[0][1]]->[A]
[[0,0][1,0]]->[B]
[[.5,.5]]->[C]
Goto OK

" Verbessertes Euler-Verfahren
Lbl 3
[[0][.5]]->[A]
[[0,0][.5,0]]->[B]
[[0,1]]->[C]
Goto OK

" Verfahren von Ralston
Lbl 4
[[0][2/3]]->[A]
[[0,0][2/3,0]]->[B]
[[1/4,3/4]]->[C]
Goto OK

" Klassisches RK-Verfahren
Lbl 5
[[0][.5][.5][1]]->[A]
[[0,0,0,0][.5,0,0,0][0,.5,0,0][0,0,1,0]]->[B]
[[1/6,1/3,1/3,1/6]]->[C]
Goto OK

" Verfahren von Bogacki/Shampine (ode23)
Lbl 6
[[0][.5][3/4][1]]->[A]
[[0,0,0,0][.5,0,0,0][0,3/4,0,0][2/9,1/3,4/9,0]]->[B]
[[2/9,1/3,4/9,0]]->[C]
Goto OK

Lbl OK

" STUFE ERMITTELN UND k-VEKTOR IN [J] INITIALISIEREN
dim([A])->L6
L6->dim([J])

" MODUS WAEHLEN
Lbl MO
ClrHome
0->F
Menu("Modus?","Runge-Kutta",RK,"RK+Fehlberg",FE,"Konvergenzord.",KO,"Ende",EN)

" FEHLBERG-FLAG SETZEN
Lbl FE
1->F

" STARTWERTE BESTIMMEN
Lbl RK
Input "Start tsmall0=",A
Input "Start ysmall0=",B
Input "Schritt h=",H
Input "Schritte m=",M
ClrHome

" ERSTES k1 BESTIMMEN
A->T
B->Y
{Y1}->[J](1,1)

" ITERATIONSSCHLEIFE
For(N,1,M)

	" Indexsymbole bestimmen
	If N=1
	Then
		"small0"->Str8
		"small1"->Str9
	End
	If N=2
	Then
		"small1"->Str8
		"small2"->Str9
	End
	If N=3
	Then
		"small2"->Str8
		"small3"->Str9
	End
	If N=4
	Then
		"small3"->Str8
		"small4"->Str9
	End
	If N=5
	Then
		"small4"->Str8
		"small5"->Str9
	End

	" k1 AUSGEBEN
	Disp "ksmall1=f(t"+Str8+",y"+Str8+")=",[J](1,1)>Frac
	Pause 
	ClrHome

	" k-VEKTOR FUELLEN
	For(I,2,L6(1))

		" Indexsymbol bestimmen
		If I=1:"small1"->Str0
		If I=2:"small2"->Str0
		If I=3:"small3"->Str0
		If I=4:"small4"->Str0
		If I=5:"small5"->Str0

		" t-WERT FUER FUNKTIONSAUSWERTUNG BESTIMMEN
		A+H[A](I,1)->T
		Disp "t=t"+Str8+"+a"+Str0+"h=",T

		" y-WERT FUER FUNKTIONSAUSWERTUNG BESTIMMEN
		B->Y
		For(J,1,I-1)
			Y+H[B](I,J)[J](J,1)->Y
		End
		Disp "y=y"+Str8+"+hSigmab"+Str0+"k=",Y>Frac

		" FUNKTIONSAUSWERTUNG
		{Y1}->[J](I,1)
		Disp "k"+Str0+"=f(t,y)=",[J](I,1)>Frac
		Pause 
		ClrHome
	End

	" VERFAHRENSFUNKTION BESTIMMEN
	det([C][J])->P
	Disp "Phi=ck=",P>Frac

	" NAECHSTER ZEITSCHRITT
	A+H->T
	B+HP->Y
	{Y1}->K
	Disp "t"+Str9+"=t"+Str8+"+h=",T
	Disp "y"+Str9+"=y"+Str8+"+hPhi=",Y>Frac
	Pause 
	ClrHome

	" FEHLBERG-TRICK
	If F=1
	Then
		Disp "Fehlberg-Trick:"
		det([D]augment([J]^^T,[[K]])^^T)->P
		B+HP->Z
		Disp "Phi=dk=",P>Frac
		Disp "z"+Str9+"=y"+Str8+"+hPhi=",Z>Frac
		Disp "e=|1-z"+Str9+"/y"+Str9+"|",abs(1-Z/Y)>Frac
		Pause 
		ClrHome
	End

	" STARTWERT UND k1 UPDATEN
	T->A
	Y->B
	K->[J](1,1)
End
Goto MO

" EINHEITSVEKTOR UND [H] = diag(a) INITIALISIEREN
Lbl KO
[A]->[I]
Fill(1,[I])
dim([B])->dim([H])
Fill(0,[H])
For(I,1,L6(1))
	[A](I,1)->[H](I,I)
End

" PRUEFE p=1
det([C][I])->C
Disp "Sigmac=",C>Frac
If C!=1
Then
	Pause "!=1, p!=1"
	Goto MO
End
Disp "p=1 OK",""

" PRUEFE p=2
det([C][A])->C
Disp "Sigmaac=",C>Frac
If C!=.5
Then
	Pause "!=1/2, p!=2" 
	Goto MO
End
Pause "p=2 OK"
ClrHome

" PRUEFE p=3
det([C][H]^2[I])->C
Disp "Sigmaa^^2c=",C>Frac
If C!=1/3
Then
	Pause "!=1/3, p!=3"
	Goto MO
End
det([C][B][A])->C
Disp "SigmaSigmacba=",C>Frac
If C!=1/6
Then
	Pause "!=1/6, p!=3"
	Goto MO
End
Pause "p=3 OK"
ClrHome

" PRUEFE p=4
det([C][H]^3[I])->C
Disp "Sigmaa^^3c=",C>Frac
If C!=1/4
Then
	Pause "!=1/4, p!=4"
	Goto MO
End
det([C][H][B][H][I])->C
Disp "SigmaSigmacaba=",C>Frac
If C!=1/8
Then
	Pause "!=1/8, p!=4"
	Goto MO
End
Pause "Weiter..."
ClrHome
det([C][B][H]^2[I])->C
Disp "SigmaSigmacba^^2=",C>Frac
If C!=1/12
Then
	Pause "!=1/12, p!=4"
	Goto MO
End
det([C][B]^2[A])->C
Disp "SigmaSigmaSigmacbba=",C>Frac
If C!=1/24
Then
	Pause "!=1/24, p!=4" 
	Goto MO
End
Pause "p=4 OK"
Goto MO

" ENDE
Lbl EN
