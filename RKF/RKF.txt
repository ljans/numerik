Lbl MS
ClrHome
Menu("Setup","f eingeben",IF,"Verf. waehlen",MV,"a eingeben",IA,"B eingeben",IB,"c eingeben",IC,"d eingeben",ID,"starten",GO)


Lbl IF
Input "f(T,Y)=",Str0
String>Equ(Str0,{Y1})
Goto MS


Lbl IA
Input "Vektor a=",[A]
ClrHome
Disp "Vektor a="
Pause [A]>Frac
Goto MS


Lbl IB
Input "Matrix B=",[B]
ClrHome
Disp "Matrix B="
Pause [B]>Frac
Goto MS


Lbl IC
Input "Vektor c=",[C]
ClrHome
Disp "Vektor c="
Pause [C]>Frac
Goto MS


Lbl ID
Input "Vektor d=",[D]
ClrHome
Disp "Vektor d="
Pause [D]>Frac
Goto MS


Lbl MV
Menu("Verfahren?","Euler1[|>]Heun2",1,"Euler2",2,"BS2[|>]Ralston3",3,"Heun2[|>]SSPRK3",4,"RK4 klassisch",5,"RK4 3/8-Regel",6,"Anderes",MS)


" Euler-Verfahren im Verfahren von Heun
Lbl 1
[[0]]->[A]
[[0]]->[B]
[[1]]->[C]
[[.5,.5]]->[D]
Goto K


" Mittelpunktsregel
Lbl 2
[[0][.5]]->[A]
[[0,0][.5,0]]->[B]
[[0,1]]->[C]
Goto K


" Verfahren von Bogacki-Shampine im Verfahren von Ralston
Lbl 3
[[0][.5][.75]]->[A]
[[0,0,0][.5,0,0][0,.75,0]]->[B]
[[2/9,1/3,4/9]]->[C]
[[7/24,1/4,1/3,1/8]]->[D]
Goto K


" Verfahren von Heun im Strong Stability Preserving Runge-Kutta-Verfahren
Lbl 4
[[0][1][.5]]->[A]
[[0,0,0][1,0,0][.25,.25,0]]->[B]
[[1/6,1/6,4/6]]->[C]
[[.5,.5,0,0]]->[D]
Goto K


" Klassisches RK-Verfahren
Lbl 5
[[0][.5][.5][1]]->[A]
[[0,0,0,0][.5,0,0,0][0,.5,0,0][0,0,1,0]]->[B]
[[1/6,1/3,1/3,1/6]]->[C]
Goto K


" 3/8-Regel
Lbl 6
[[0][1/3][2/3][1]]->[A]
[[0,0,0,0][1/3,0,0,0][~1/3,1,0,0][1,~1,1,0]]->[B]
[[1/8,3/8,3/8,1/8]]->[C]
Goto K


" Butcher-Tableau kontrollieren
Lbl K
Disp "Vektor a="
Pause [A]>Frac
ClrHome
Disp "Matrix b="
Pause [B]>Frac
ClrHome
Disp "Vektor c="
Pause [C]>Frac
ClrHome
Disp "Vektor d="
Pause [D]>Frac
Goto MS


" Stufe ermitteln und k-Vektor initialisieren
Lbl GO
dim([A])->|LDIM:|LDIM(1)->S
[A]->[J]


Disp "Anfangswert:"
Input "tsmall0=",A
Input "ysmall0=y(tsmall0)=",B
ClrHome


" Funktion fuer erstes k1 auswerten
A->T
B->Y
{Y1}->K


" Indizes bestimmen
0->N
"small0"->Str9
Lbl S
N+1->N
Str9->Str8
If N=1:"small1"->Str9
If N=2:"small2"->Str9
If N=3:"small3"->Str9
If N=4:"small4"->Str9
If N=5:"small5"->Str9


Disp "Schrittweite:"
Input "h=",H
ClrHome


" Startwerte und k1 uebernehmen
T->A
Y->B
K->[J](1,1)
Disp "ksmall1=f(t"+Str8+",y"+Str8+")=",K>Frac
Pause 
ClrHome


" k-Vektor fuellen
"small2"->Str0
For(I,2,S)

	
	" t fuer Funktionsauswertung bestimmen
	A+H[A](I,1)->T
	Disp "t=t"+Str8+"+a"+Str0+"h=",T

	
	" y fuer Funktionsauswertung bestimmen
	B->Y
	For(J,1,I-1)
		Y+H[B](I,J)[J](J,1)->Y
	End
	Disp "y=y"+Str8+"+hSigmab"+Str0+"k=",Y>Frac

	
	" Funktion auswerten
	{Y1}->[J](I,1)
	Disp "k"+Str0+"=f(t,y)=",[J](I,1)>Frac
	Pause 
	ClrHome


	" Naechsten Index bestimmen
	If I=2:"small3"->Str0
	If I=3:"small4"->Str0
	If I=4:"small5"->Str0
	If I=5:"small6"->Str0
End


" Phi bestimmen
det([C][J])->P
Disp "Phi=ck=",P>Frac


" Startwerte und k1 aktualisieren
A+H->T
B+HP->Y
{Y1}->K
Disp "t"+Str9+"=t"+Str8+"+h=",T
Disp "y"+Str9+"=y"+Str8+"+hPhi=",Y>Frac


Lbl MA
Pause 
ClrHome
Menu("Aktion?","fortschreiten",S,"Fehlberg-Trick",FT,"p(c) bestimmen",P,"beenden",E)


Lbl FT
Disp "k"+Str0+"=f(t"+Str9+",y"+Str9+")=",K
det([D]augment([J]^^T,[[K]])^^T)->P
B+HP->Z
Disp "Phi=dk=",P>Frac
Disp "z"+Str9+"=y"+Str8+"+hPhi=",Z>Frac
Pause 
ClrHome
Disp "Falls p(d)<p(c):","e=|1-z"+Str9+"/y"+Str9+"|",abs(1-Z/Y)>Frac,""
Disp "Falls p(d)>p(c):","e=|1-y"+Str9+"/z"+Str9+"|",abs(1-Y/Z)>Frac
Goto MA


" Einheitsvektor und A = diag(a) initialisieren
Lbl P
[A]->[I]
Fill(1,[I])
dim([B])->dim([H])
Fill(0,[H])
For(I,1,S)
	[A](I,1)->[H](I,I)
End


" Teste p=1
det([C][I])->C
Disp "Sigmac=",C>Frac
If C!=1:Then
	Disp "!=1, p!=1"
	Goto MA
End
Disp "p=1 OK",""


" Teste p=2
det([C][A])->C
Disp "Sigmaac=",C>Frac
If C!=.5:Then
	Disp "!=1/2, p!=2"
	Goto MA
End
Pause "p=2 OK"
ClrHome


" Teste p=3
det([C][H]^2[I])->C
Disp "Sigmaa^^2c=",C>Frac
If C!=1/3:Then
	Disp "!=1/3, p!=3"
	Goto MA
End
det([C][B][A])->C
Disp "SigmaSigmacba=",C>Frac
If C!=1/6:Then
	Disp "!=1/6, p!=3"
	Goto MA
End
Pause "p=3 OK"
ClrHome


" Teste p=4
det([C][H]^3[I])->C
Disp "Sigmaa^^3c=",C>Frac
If C!=1/4:Then
	Disp "!=1/4, p!=4"
	Goto MA
End
det([C][H][B][H][I])->C
Disp "SigmaSigmacaba=",C>Frac
If C!=1/8:Then
	Disp "!=1/8, p!=4"
	Goto MA
End
Pause "Weiter..."
ClrHome
det([C][B][H]^2[I])->C
Disp "SigmaSigmacba^^2=",C>Frac
If C!=1/12:Then
	Disp "!=1/12, p!=4"
	Goto MA
End
det([C][B]^2[A])->C
Disp "SigmaSigmaSigmacbba=",C>Frac
If C!=1/24:Then
	Disp "!=1/24, p!=4" 
	Goto MA
End
Disp "p=4 OK"
Goto MA


Lbl E
