" SETUP
0->K
"small0"->Str0
Lbl MS
ClrHome
Menu("Setup","A eingeben",IA,"b eingeben",IB,"xsmall0 eingeben",IC,"starten",GO)

" Matrix eingeben
Lbl IA
Input "Matrix A=",[A]
Goto MS

" Rechte Seite eingeben
Lbl IB
Input "Rechte Seite b=",[B]
Goto MS

" Startwert eingeben
Lbl IC
Input "Startwert xsmall0=",[C]
Goto MS

" INITIALISIERUNG
Lbl GO
[C]->[D]
dim([A])->|LDIM:|LDIM(1)->N

" D, L, R BESTIMMEN
[A]->[H]
[A]->[I]
[A]->[J]
For(I,1,N)
	For(J,1,N)
		If I!=J:0->[H](I,J)
		If I<=J:0->[I](I,J)
		If I>=J:0->[J](I,J)
	End
End

" VERFAHREN WAEHLEN
Lbl MV
1->W
Menu("Verfahren?","Gausharps-Seidel",GS,"SOR 0<w<2",IW,"Jacobi",J)

" Successive Over-Relaxation
Lbl IW
Input "Relaxation w=",W
ClrHome

" Gauss-Seidel
Lbl GS
W([H]+W[I])^^-1->[G]
(~1/W[G])((W-1)[H]+W[J])->[F]
If W=1:Disp "M=~(D+L)^^-1R="
If W!=1:Disp "M=~(D+wL)^^-1","*((w-1)D+wR)="
Pause [F]
ClrHome
If W=1:Disp "N=(D+L)^^-1="
If W!=1:Disp "N=w(D+wL)^^-1="
Pause [G]
Goto L

" Jacobi
Lbl J
[H]^^-1->[G]
~[G]*([I]+[J])->[F]
Disp "M=~D^^-1(L+R)="
Pause [F]
ClrHome
Disp "N=D^^-1="
Pause [G]
Goto L

" ZEILEN- UND SPALTENSUMMENNORM BERECHNEN
Lbl L
ClrHome
0->Z
0->S
For(I,1,N)

	" Zwischensummen
	0->C
	0->D
	For(J,1,N)
		C+abs([F](I,J))->C
		D+abs([F](J,I))->D
	End

	" Neue maximale Werte
	If C>Z:C->Z
	If D>S:D->S
End

" KONVERGENZTEST MIT ZEILENSUMMENNORM
Disp "Zeilen |M|small0small0=",Z>Frac
If Z<1:Then
	Pause "=L<1 geeignet"
	Z->L
	0->P
	Goto IT
End
Pause ">=1 ungeeignet"
ClrHome

" KONVERGENZTEST MIT SPALTENSUMMENNORM
Disp "Spalten |M|small1=",S>Frac
If S<1:Then
	Pause "=L<1 geeignet"
	S->L
	1->P
	Goto IT
End
Pause ">=1 ungeeignet"
ClrHome

" EINGABE DER SPEKTRALNORM
Input "Spektral |M|small2=",L
2->P

" ITERATIONSSCHRITT
Lbl IT
K+1->K
Str0->Str9
If K=1:"small1"->Str0
If K=2:"small2"->Str0
If K=3:"small3"->Str0
If K=4:"small4"->Str0
If K=5:"small5"->Str0
If K>5:""->Str0
ClrHome

" NEUE ITERIERTE BERECHNEN
[D]->[E]
[F][D]+[G][B]->[D]
Disp "x"+Str0+"=Mx"+Str9+"+Nb=",[D]

" AENDERUNG IN P-NORM BERECHNEN
0->D
For(I,1,N)
	abs([D](I,1)-[E](I,1))->C
	If P=0 and C>D:C->D
	If P=1:D+C->D
	If P=2:D+C^2->D
End
If P=2:sqrt(D)->D

" AKTION WAEHLEN
Lbl MA
Pause 
ClrHome
Menu("Aktion?","iterieren",IT,"a-priori",PR,"a-posteriori",PO,"wechseln",MV,"beenden",E)

" A-PRIORI-FEHLERSCHAETZUNG
Lbl PR
Input "Toleranz epsilon=",E
Disp "Schritte k>=",ln((1-L)E/D)/ln(L)+K-1
Goto MA

" A-POSTERIORI-FEHLERSCHAETZUNG
Lbl PO
Disp "|x"+Str0+"-x*|"+"<=","L/(1-L)|x"+Str0+"-x"+Str9+"|=",DL/(1-L)
Goto MA

" ENDE
Lbl E
